buildscript {
    repositories {
        maven { url "http://dl.bintray.com/jetbrains/intellij-plugin-service" }
    }
}

plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "maven"
    id "org.jetbrains.intellij" version "0.1.10"
}

allprojects {
    group "org.metaborg"
    version "2.1.0-SNAPSHOT"
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

intellij {
    version = "2016.2.3"
    type = "IC"

    // FIXME: hack to support both IDEA 15 and IDEA 16.
    // See https://github.com/intellij-rust/intellij-rust/issues/243
    updateSinceUntilBuild = false
}

ext {
    metaborgVersion = "2.1.0-SNAPSHOT"
    guiceVersion = "4.0"
}

configurations {
    provided
    language
    languageBaseline
    testLanguage
    testLanguageBaseline
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "http://artifacts.metaborg.org/content/repositories/releases/"
    }
    maven {
        url "http://artifacts.metaborg.org/content/repositories/snapshots/"
    }
}

// To debug the JPS plugin: enable the "Debug Build Process" action in the sandbox instance
// (Ctrl+Shift+A), then start a build and attach a remote debugger to port 5005.
// https://github.com/JetBrains/gradle-intellij-plugin/issues/23
afterEvaluate {
    tasks.getByName('runIdea') {
        jvmArgs += ["-Dcompiler.process.debug.port=5005"]
    }
}

task copyLanguageDependencies(type: Copy, description: 'Copies the language dependencies to the resources.') {
    into "src/main/resources/languages/"
    from configurations.language
}

task copyTestLanguageDependencies(type: Copy, description: 'Copies the language dependencies to the test resources.') {
    into "src/test/resources/languages/"
    from configurations.testLanguage
}

task copyLanguageBaselineDependencies(type: Copy, description: 'Copies the language dependencies to the resources.', dependsOn: copyLanguageDependencies) {
    into "src/main/resources/languages/"
    from configurations.languageBaseline
}

task copyTestLanguageBaselineDependencies(type: Copy, description: 'Copies the language dependencies to the test resources.', dependsOn: copyTestLanguageDependencies) {
    into "src/test/resources/languages/"
    from configurations.testLanguageBaseline
}

processResources.dependsOn copyLanguageBaselineDependencies
processTestResources.dependsOn copyTestLanguageBaselineDependencies

dependencies {
    compile     project(":org.metaborg.intellij-common")
    // Main dependencies
    compile     ("org.metaborg:org.metaborg.jps:$metaborgVersion") {
        // Fixes an issue where the IntelliJ sources cause class loading to fail.
        //   ClassNotFoundException: org.jetbrains.jps.intellilang.model.impl.JpsIntelliLangModelSerializerExtension
        exclude module: "idea"
    }
    compile     ("org.metaborg:org.metaborg.core:$metaborgVersion") {
        exclude group: "org.slf4j"
        exclude group: "com.google.guava"
    }
    compile     ("org.metaborg:org.metaborg.spoofax.core:$metaborgVersion") {
        exclude group: "org.slf4j"
        exclude group: "com.google.guava"
    }
    compile     ("org.metaborg:org.metaborg.spoofax.meta.core:$metaborgVersion") {
        exclude group: "org.slf4j"
        exclude group: "com.google.guava"
    }
    provided    "com.google.code.findbugs:jsr305:3.0.0"
    provided    "com.google.guava:guava:17.0"

    provided    "org.slf4j:slf4j-api:1.7.10"
    compile     "org.apache.commons:commons-lang3:3.4"
    compile     "org.apache.commons:commons-vfs2:2.1"
    compile     "com.google.inject:guice:$guiceVersion"
    compile     "com.google.inject.extensions:guice-assistedinject:$guiceVersion"
    compile     "com.google.inject.extensions:guice-multibindings:$guiceVersion"
    compile     "org.javassist:javassist:3.20.0-GA"

    language    "org.metaborg:org.metaborg.meta.lang.esv:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.nabl:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.sdf:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.template:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.ts:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.stratego:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.analysis:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lib.analysis:$metaborgVersion@spoofax-language"

    testCompile "junit:junit:4.12"

    testLanguage "org.metaborg:org.metaborg.meta.lang.sdf:$metaborgVersion@spoofax-language"
}

sourceSets.main.compileClasspath += configurations.provided

idea {
    module {
        inheritOutputDirs = true
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

eclipse {
    classpath {
        plusConfigurations += [configurations.provided]
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            mavenLocal()
        }
    }
}
