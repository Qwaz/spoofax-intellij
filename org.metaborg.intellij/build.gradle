buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    maven {
      url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
    }

  }
  dependencies {
    classpath "gradle.plugin.org.jetbrains:gradle-intellij-plugin:0.1.10"
  }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven'

    ext {
        metaborgVersion = "2.1.0-SNAPSHOT"
        guiceVersion = "4.0"
    }

    group "org.metaborg"
    version "$metaborgVersion"
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "http://artifacts.metaborg.org/content/repositories/releases/"
        }
        maven {
            url "http://artifacts.metaborg.org/content/repositories/snapshots/"
        }
        maven {
            url "https://raw.githubusercontent.com/pluto-build/pluto-build.github.io/master/mvnrepository/"
        }
        maven {
            url "http://sugar-lang.github.io/mvnrepository/"
        }
    }

    configurations {
        provided
    }

    sourceSets.main.compileClasspath += configurations.provided

    idea {
        module {
            inheritOutputDirs = true
            scopes.PROVIDED.plus += [configurations.provided]
        }
    }

    eclipse {
        classpath {
            plusConfigurations += [configurations.provided]
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                mavenLocal()
            }
        }
    }
}

configure(allprojects.findAll {it.name != 'org.metaborg.spoofax-deps'}) {
  apply plugin: 'org.jetbrains.intellij'

  intellij {
      version = "2016.3"

      // FIXME: hack to support both IDEA 15 and IDEA 16.
      // See https://github.com/intellij-rust/intellij-rust/issues/243
      updateSinceUntilBuild = false
  }
}

intellij {
    type = "IC"
}

configurations {
    language
    languageBaseline
    testLanguage
    testLanguageBaseline
}

// To debug the JPS plugin: enable the "Debug Build Process" action in the sandbox instance
// (Ctrl+Shift+A), then start a build and attach a remote debugger to port 5005.
// https://github.com/JetBrains/gradle-intellij-plugin/issues/23
afterEvaluate {
    tasks.getByName('runIdea') {
        jvmArgs += ["-Dcompiler.process.debug.port=5005"]
    }
}

task copyLanguageDependencies(type: Copy, description: 'Copies the language dependencies to the resources.') {
    into "src/main/resources/languages/"
    from configurations.language
}

task copyTestLanguageDependencies(type: Copy, description: 'Copies the language dependencies to the test resources.') {
    into "src/test/resources/languages/"
    from configurations.testLanguage
}

task copyLanguageBaselineDependencies(type: Copy, description: 'Copies the language dependencies to the resources.', dependsOn: copyLanguageDependencies) {
    into "src/main/resources/languages/"
    from configurations.languageBaseline
}

task copyTestLanguageBaselineDependencies(type: Copy, description: 'Copies the language dependencies to the test resources.', dependsOn: copyTestLanguageDependencies) {
    into "src/test/resources/languages/"
    from configurations.testLanguageBaseline
}

processResources.dependsOn copyLanguageBaselineDependencies
processTestResources.dependsOn copyTestLanguageBaselineDependencies

dependencies {
    // Main dependencies
    compile     project(":org.metaborg.spoofax-common")
    compile     (project(":org.metaborg.jps")) {
        // Fixes an issue where the IntelliJ sources cause class loading to fail.
        //   ClassNotFoundException: org.jetbrains.jps.intellilang.model.impl.JpsIntelliLangModelSerializerExtension
        exclude module: "idea"
    }

    language    "org.metaborg:org.metaborg.meta.lang.esv:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.nabl:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.sdf:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.template:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.ts:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.stratego:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.analysis:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lib.analysis:$metaborgVersion@spoofax-language"

    testCompile "junit:junit:4.12"

    testLanguage "org.metaborg:org.metaborg.meta.lang.sdf:$metaborgVersion@spoofax-language"
}
